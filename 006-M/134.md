0xlmanini

medium

# Erc20CheckedTransfer library doesn't handle non-standard tokens correctly

## Summary
[Erc20CheckedTransfer.sol](https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/Erc20CheckedTransfer.sol) provides two methods `checkedTransfer()` and `checkedTransferFrom()`, which both expect a given ERC20's `transfer()` and `transferFrom()` to return a boolean.
This known issue prohibits the system to use tokens such as USDT that do not conform to the ERC20 standard by not returning a boolean on such methods.

## Vulnerability Detail
The implemented library will cause a transactions involved with a non-standard ERC20 to always revert, going against the system's intention of allowing any type of collateral and token to be used in a loan.

## Impact
Violation of system assumption, broken interoperability.

## Code Snippet
The faulty methods can be found [here](https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/Erc20CheckedTransfer.sol#L9-L19)

To verify the finding, I've implemented a short, non-standard ERC20 token and tested the library against it.

In `test/Commons/` I added the contract
```solidity
pragma solidity ^0.8.0;

contract MissingReturnToken {
    // --- ERC20 Data ---
    string  public constant name = "Token";
    string  public constant symbol = "TKN";
    uint8   public constant decimals = 18;

    mapping (address => uint)                      public balanceOf;
    mapping (address => mapping (address => uint)) public allowance;

    // --- Init ---
    constructor() {}

    // --- Token ---
    function transfer(address dst, uint wad) external {
        transferFrom(msg.sender, dst, wad);
    }
    function transferFrom(address src, address dst, uint wad) public {
        require(balanceOf[src] >= wad, "insufficient-balance");
        if (src != msg.sender && allowance[src][msg.sender] != type(uint).max) {
            require(allowance[src][msg.sender] >= wad, "insufficient-allowance");
            allowance[src][msg.sender] -= wad;
        }
        balanceOf[src] -= wad;
        balanceOf[dst] += wad;
    }
    function approve(address usr, uint wad) external {
        allowance[msg.sender][usr] = wad;
    }
    function mintTo(address usr, uint wad) external {
        balanceOf[usr] += wad;
    }
}
```

and in `test/` I added the following tests as a PoC of my finding
```solidity
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "./Commons/MissingReturnERC20.sol";
import "../src/utils/Erc20CheckedTransfer.sol";

contract TestMissingReturns is Test {
    using Erc20CheckedTransfer for IERC20;

    MissingReturnToken token;
    address alice;
    address bob;
    address charlie;

    function setUp() external {
        alice = makeAddr("alice");
        bob = makeAddr("bob");
        charlie = makeAddr("charlie");

        token = new MissingReturnToken();
        token.mintTo(alice, 3 ether);
    }

    function test_verifyMissingReturnBreaksTransfer() external {
        address tokenAddr = address(token);
        
        // checkedTransfer reverts
        vm.prank(alice);
        IERC20(tokenAddr).checkedTransfer(bob, 1 ether);
    }

    function test_verifyMissingReturnBreaksTransferFrom() external {
        address tokenAddr = address(token);

        // checkedTransferFrom reverts
        vm.prank(alice);
        token.approve(charlie, 1 ether);
        vm.prank(charlie);
        IERC20(tokenAddr).checkedTransferFrom(alice, bob, 1 ether);
    }
}
```

Notice that, when running `forge test`, the two test cases will revert with an `EvmError: Revert` error. At the time of writing, adding `vm.expectRevert()` before the calls to the library methods doesn't accurately catch the revert, as the revert reason will change to `Call did not revert as expected` which seems to be a bug in the foundry testing tool.
Nonetheless, the bug is verified.

## Tool used

Manual Review, Foundry tests

## Recommendation
Switch to Openzeppelin's standard `SafeERC20` library