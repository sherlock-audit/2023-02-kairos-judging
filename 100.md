chaduke

medium

# checkedTransferFrom() and checkedTransfer() will break for tokens that do not return a boolean value for transferFrom() and transfer().

## Summary
``checkedTransferFrom()`` and ``checkedTransfer()`` will break for tokens that do not return a boolean value for ``transferFrom()`` and ``transfer()``.

## Vulnerability Detail

``checkedTransferFrom()`` and ``checkedTransfer()`` are used to transfer an arbitrary ERC20 tokens from one user to another. 

[https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/Erc20CheckedTransfer.sol#L9-L19](https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/Erc20CheckedTransfer.sol#L9-L19)

Both of them assume that the underlying implementation of ``transfer()`` and ``transferFrom()`` will return a bool value to indicate if the call is successful. However, some ERC20 tokens will simply not return truth values when they are successful and revert when there is a failure. 

The problem is when the the call to ``transfer()`` and ``transferFrom()`` are successful but do  not return any bool value, ``checkedTransferFrom()`` and ``checkedTransfer()`` will still fail since they expect to receive a true value in this case. 

In summary, ``checkedTransferFrom()`` and ``checkedTransfer()`` will break when working with ERC20 tokens that will not return truth values for ``transfer()`` and ``transferFrom``.

The following code POC confirms my finding:

```javascript
function testTranferOnWSU() public {
        wsu.transfer(address(2), 1000);
        assertEq(wsu.balanceOf(address(2)), 1000);
        vm.prank(address(2));
        wsu.approve(address(this), 1000);
        wsu.transferFrom(address(2), address(3), 500);
        assertEq(wsu.balanceOf(address(3)), 500);
        assertEq(wsu.balanceOf(address(2)), 500);
        vm.expectRevert();
        wsu.checkedTransferFrom(IERC20(address(wsu)), address(2), address(3), 500);
    }

// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.17;

interface IERC20 { // five  functions and four implicit getters
    function totalSupply() external view returns (uint);
    function balanceOf(address tokenOwner) external view returns (uint balance);
    function allowance(address tokenOwner, address spender) external view returns (uint remaining);
    function transfer(address to, uint rawAmt) external returns (bool success);
    function approve(address spender, uint rawAmt) external returns (bool success);
    function transferFrom(address from, address to, uint rawAmt) external returns (bool success);

    event Transfer(address indexed from, address indexed to, uint rawAmt);
    event Approval(address indexed tokenOwner, address indexed spender, uint rawAmt);
    
}



contract WSUToken{
    string public name;
    string public symbol;
    uint8 public decimals; // 18 decimals is the strongly suggested default, avoid changing it

    uint256 public _totalSupply;

    mapping(address => uint) balances;
    mapping(address => mapping(address => uint)) allowed;

    event Transfer(address indexed from, address indexed to, uint tokens);
    event Approval(address indexed tokenOwner, address indexed spender, uint tokens);
    error ERC20TransferFailed(IERC20 token, address from, address to);

    /**
     * Constrctor function
     *
     * Initializes contract with initial supply tokens to the creator of the contract
     */
    constructor(){
        name = "WSU Token";
        symbol = "WSU";
        decimals = 18;
        _totalSupply = 1000000000*10**18; // 1 billion tokens

        balances[msg.sender] = _totalSupply;
        emit Transfer(address(0), msg.sender, _totalSupply);
    }

    function totalSupply() public view returns (uint) {
        return _totalSupply;
    }

    function balanceOf(address tokenOwner) public view returns (uint balance) {
        return balances[tokenOwner];
    }

    function allowance(address tokenOwner, address spender) public view returns (uint remaining) {
        return allowed[tokenOwner][spender];
    }

    function approve(address spender, uint256 tokens) public returns (bool success) {
        allowed[msg.sender][spender] = tokens;
        emit Approval(msg.sender, spender, tokens);
        return true;
    }

    function transfer(address to, uint256 tokens) public{
        balances[msg.sender] = balances[msg.sender] - tokens;
        balances[to] = balances[to] + tokens;
        emit Transfer(msg.sender, to, tokens);
    }

    function transferFrom(address from, address to, uint256 tokens) public{
        if(balances[from] < tokens) revert();

        balances[from] = balances[from] - tokens;
        allowed[from][msg.sender] = allowed[from][msg.sender] -  tokens;
        balances[to] = balances[to] + tokens;
        emit Transfer(from, to, tokens);
    }

    function checkedTransferFrom(IERC20 currency, address from, address to, uint256 amount) public {
        if (!currency.transferFrom(from, to, amount)) {
            revert ERC20TransferFailed(currency, from, to);
        }
    }

    function checkedTransfer(IERC20 currency, address to, uint256 amount) public {
        if (!currency.transfer(to, amount)) {
            revert ERC20TransferFailed(currency, address(this), to);
        }
    }
}

```

## Impact
``checkedTransferFrom()`` and ``checkedTransfer()`` will break when working with ERC20 tokens that will not return truth values for ``transfer()`` and ``transferFrom``.


## Code Snippet
See above

## Tool used
VSCode

Manual Review

## Recommendation
Use OpenZeppelin's safeTransfer() and safeTransferFrom() which consider both cases: 1) the underlying ERC20 token  returns truth values for transfer() and transferFrom(); and 2) the underlying ERC20 token does not  return truth values for transfer() and transferFrom().