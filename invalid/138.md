sashik_eth

medium

# Lenders risk losing interest when loanToValue equals the fair market price of NFTs

## Summary

Borrower has no economic motivation to repay loans when the price of NFT is lower than the previously received loan plus interest.  This could leave the lender without the expected profit, while the borrower effectively obtains an interest-free loan.

## Vulnerability Detail

Kairos documentation includes [fact](https://doc.kairos.loan/in-depth/supplying.html) that should be guaranteed by protocol:
**Kairos guarantees that the lender is at a loss only if it made a mistake by signing an offer with a ```loanToValue``` field higher for the corresponding NFT than the fair market price of this NFT at any point in time during the loan lifetime (including auction time).** 

However, if the lender creates an offer with a ```loanToValue``` parameter equal to the fair market price and the borrower does not repay, the NFT would be sold at auction for its fair market price, leaving the lender without the earned interest. Furthermore, in this situation, the borrower would be motivated to buy their own loan at auction until its market price is lower than the borrowed amount plus interest debt.

So to ensure that the lender would not lose any profit ```loanToValue``` should be not only lower or equal to the fair market price of NFT (like the statement above says) but also lower than the price of NFT minus the potential amount of interest (tranche * loan duration). 

As a POC consider the next scenario with NFT that has a static market price (2 ETH) during all loan lifetime:

1. Alice creates an offer for this NFT, setting loanToValue with 2 ETH, duration 1 year, and 40% APY tranche, expecting an additional profit of 0.8 ETH if the loan lasts the full duration.
2. Bob borrows against Alice's offer, receives 2 ETH, and provides NFT as collateral.
3. After one year Bob should repay 2.8 ETH to receive NFT back, but since the market price is still 2 ETH - he decides to wait until the auction starts for his loan and buys it at a fair price of 2 ETH.
4. Alice claims her shares for a loan in the amount of 2 ETH while the expected received value was 2.8 ETH. Bob on his side received an interest-free loan.

## Impact

Lenders may consistently miss out on interest earnings when setting the ```Offer.loanToValue``` parameter higher than the fair market price of the NFT minus the potential interest amount.

## Code Snippet

```repay()``` in RepayFaucet.sol calculates the amount to be repaid by the borrower including ```loan.lent``` amount and accrued interests:

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/RepayFacet.sol#L24

```solidity
    function repay(uint256[] memory loanIds) external {
        Protocol storage proto = protocolStorage();
        Loan storage loan;
        uint256 lent;
        uint256 interests;
        uint256 toRepay;

        for (uint256 i = 0; i < loanIds.length; i++) {
            loan = proto.loan[loanIds[i]];
            // loan.payment.paid may be at 0 and considered repaid in case of an auction sale executed at price 0
            if (loan.payment.paid > 0 || loan.payment.borrowerClaimed || loan.payment.liquidated) {
                revert LoanAlreadyRepaid(loanIds[i]);
            }
            lent = loan.lent;
            /* if the linear interests are very low due to a short time elapsed, the minimal interests amount to repay
            is applied as an anti ddos mechanism */
            interests = RayMath.max(
                /* during the interests calculus, we can consider that (block.timestamp - loan.startDate)
                won't exceed 1e10 (>100 years) and interest per second (unwrapped value) won't exceed
                1e27 (corresponding to an amount to repay doubling after 1 second), we can deduce that
                (loan.interestPerSecond.mul(block.timestamp - loan.startDate)) is capped by 1e10 * 1e27 = 1e37
                we want to avoid the interests calculus to overflow so the result must not exceed 1e77
                as (1e77 < type(uint256).max). So we can allow `lent` to go as high as 1e40, but not above.
                This explains why borrowing throws on loan.lent > 1e40, as this realisticly avoids
                repaying being impossible due to an overflow. */
                /* the interest per second is a share of what has been lent to add to the interests each second. The
                next line accrues linearly */
                lent.mul(loan.interestPerSecond.mul(block.timestamp - loan.startDate)),
                loan.payment.minInterestsToRepay
            );
            toRepay = lent + interests;
            loan.payment.paid = toRepay;
            loan.payment.borrowerClaimed = true;
            loan.assetLent.checkedTransferFrom(msg.sender, address(this), toRepay);
            loan.collateral.implem.safeTransferFrom(address(this), loan.borrower, loan.collateral.id);
            emit Repay(loanIds[i]);
        }
    }
```
while ```price()``` in AuctionFaucet.sol calculates NFT price based only on ```loan.lent``` amount, without accrued interest:

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/AuctionFacet.sol#L34

```solidity
    function price(uint256 loanId) public view returns (uint256) {
        Loan storage loan = protocolStorage().loan[loanId];
        uint256 loanEndDate = loan.endDate;
        uint256 timeSinceLiquidable = block.timestamp - loanEndDate;

        checkLoanStatus(loanId);

        /* the decreasing factor controls the evolution of the price from its initial value to 0 (and staying at 0)
        over the course of the auction duration */
        Ray decreasingFactor = timeSinceLiquidable >= loan.auction.duration
            ? ZERO
            : ONE.sub(timeSinceLiquidable.div(loan.auction.duration));

        /* the estimated value arises from the mean of the loan offer loanToValues used in the loan regarding their
        share in the collateral usage. This must stay consitent even if less than the full value of the NFT has been
        used as collateral */
        uint256 estimatedValue = loan.lent.mul(ONE.div(loan.shareLent));

        /* by mutliplying the estimated price by some factor and slowly decreasing this price over time we aim to
        make sure a liquidator will buy the NFT at fair market price. */
        return estimatedValue.mul(loan.auction.priceFactor).mul(decreasingFactor);
    }
```


## Tool used
Manual Review

## Recommendation

Consider explicitly informing lenders that they must account for the potential interest amount when they decide on their ```Offer.loanToValue``` parameter or update BorrowerHandlers.sol code so borrower receive ```loanToValue``` - ```tranche``` * ```duration``` and save accordingly value to ```lent``` loan field.
