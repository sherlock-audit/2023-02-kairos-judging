GimelSec

high

# `ClaimFaucet.sendInterests` could be unfair if `interests == loan.payment.minInterestsToRepay`

## Summary

`minInterestsToRepay` is used as an anti-ddos mechanism to prevent borrowers from producing lots of dust supply positions that the lenders will have to pay gas to claim. But the mechanism could be unfair in some cases.

## Vulnerability Detail

`minInterestsToRepay` is used as an anti-ddos mechanism in `BorrowHandler.initializedLoan()`.
https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/BorrowLogic/BorrowHandlers.sol#L180
```solidity
    function initializedLoan(
        CollateralState memory collatState,
        address from,
        NFToken memory nft,
        uint256 nbOfOffers,
        uint256 lent,
        uint256 firstSupplyPositionId
    ) internal view returns (Loan memory) {
        …

        /* the minimum interests amount to repay is used as anti ddos mechanism to prevent borrowers to produce lots of
        dust supply positions that the lenders will have to pay gas to claim. This is why it is determined on a
        per-offer basis, as each position can be used to claim funds separetely and induce a gas cost. With a design
        approach similar to the auction parameters setting, this minimal cost is set at borrow time to avoid bad
        surprises arising from governance setting new parameters during the loan life. cf docs for more details. */
        notPaid.minInterestsToRepay = nbOfOffers * proto.minOfferCost[collatState.assetLent];

        return
            Loan({
                assetLent: collatState.assetLent,
                lent: lent,
                shareLent: collatState.matched,
                startDate: block.timestamp,
                endDate: endDate,
                /* auction parameters are copied from protocol parameters to the loan storage as a way to prevent
                a governance-initiated change of terms to modify the terms a borrower chose to accept or change the
                price of an NFT being sold abruptly during the course of an auction. */
                auction: Auction({duration: proto.auction.duration, priceFactor: proto.auction.priceFactor}),
                /* the interest rate is stored as a value instead of the tranche id as a precaution in case of a change
                in the interest rate mechanisms due to contract upgrade */
                interestPerSecond: proto.tranche[collatState.tranche],
                borrower: from,
                collateral: nft,
                supplyPositionIndex: firstSupplyPositionId,
                payment: notPaid,
                /* from the first supply position id and the number of offers used all supply position ids can be
                deduced + the number of offers/positions is directly accessible for other purposes */
                nbOfPositions: nbOfOffers
            });
    }
```

And if the total interest is equal to `minInterestsToRepay`. each lender gets the same minimal interest.
https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/ClaimFacet.sol#L94
```solidity
    function sendInterests(Loan storage loan, Provision storage provision) internal returns (uint256 sent) {
        uint256 interests = loan.payment.paid - loan.lent;
        if (interests == loan.payment.minInterestsToRepay) {
            // this is the case if the loan is repaid shortly after issuance
            // each lender gets its minimal interest, as an anti ddos measure to spam offer
            sent = provision.amount + (interests / loan.nbOfPositions);
        } else {
            /* provision.amount / lent = share of the interests belonging to the lender. The parenthesis make the
            calculus in the order that maximizes precison */
            sent = provision.amount + (interests * (provision.amount)) / loan.lent;
        }
        loan.assetLent.checkedTransfer(msg.sender, sent);
    }
```

However, a bad borrower can use this mechanism to make lenders receive less interests.

Suppose that Alice is a borrower and Bob is a lender:
* Bob signs an offer O1 with loanToValue = 100 USDC
* Alice has the NFT that Bob wants
* Alice signs another offer O2  with loanToValue = 100 USDC
* Alice calls `BorrowFacet.borrow()` with the two above offers.
* Alice set O1’s amount to 99 USDC and O2’s amount to 1.
* Suppose that  proto.minOfferCost[USDC] = 20. `minInterestsToRepay` is 40.
* When the interest goes to 40, Alice calls `RepayFacet.repay()`.
* Bob can only receive 99+20 USDC. But Bob should receive more.

So bad borrowers can leverage this mechanism to make lenders receive less than lenders should receive. The lenders cannot receive more interests than `proto.minOfferCost[collatState.assetLent]`

## Impact

Bad borrowers can make lenders receive less interests.

## Code Snippet

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/ClaimFacet.sol#L94
https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/BorrowLogic/BorrowHandlers.sol#L180


## Tool used

Manual Review

## Recommendation

The `minInterestsToRepay` mechanism should take the amount or the matched shares into consideration.

