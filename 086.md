7siech

high

# Total loss of funds when using certain popular ERC20 tokens

## Summary
It is possible for funds to get stuck in the contract when using certain popular ERC20 tokens that do not return a boolean value for `transfer` but do return a value for `transferFrom` ex. BNB token (market cap of $50B).

## Vulnerability Detail

The project uses a custom library `Erc20CheckedTransfer` [0] to transfer ERC-20 tokens. However, the implementation has a bug where it does not account for popular tokens not returning a boolean [1] resulting in lost funds.

Ex. the BNB token returns a boolean for `transferFrom` but not for `transfer` calls. This results in the project working correctly to borrow and repay loans, but not to `claim` funds thus the tokens are stuck in the project's contract.

## Impact

Loss of funds for both borrowers and lenders when trying to `claim`/`claimAsBorrower`.

## Code Snippet

Vulnerable code [0]

```solidity
library Erc20CheckedTransfer {
    function checkedTransferFrom(IERC20 currency, address from, address to, uint256 amount) internal {
        if (!currency.transferFrom(from, to, amount)) {
            revert ERC20TransferFailed(currency, from, to);
        }
    }

    function checkedTransfer(IERC20 currency, address to, uint256 amount) internal {
        if (!currency.transfer(to, amount)) {
            revert ERC20TransferFailed(currency, address(this), to);
        }
    }
}
```

POC snippet [2]

```solidity
contract LostFunds is External {
  using RayMath for Ray;
  using RayMath for uint256;

  address bnb = 0xB8c77482e45F1F44dE1745F52C74426C631bDD52;
  address bnbWhale = 0xf2549FBA1DA6E17a1e82478A0b0a945aDB7416c7;

  function setUp() override public {
    vm.createSelectFork("mainnet");

    vm.label(bnb, "bnb");
    vm.label(bnbWhale, "bnbWhale");

    super.setUp();

    // set money to BNB
    money = Money(bnb);
  }

  // @audit adapted to work with forked tokens
  function getFlooz(address to, Money moula, uint256 amount) virtual override internal {
    vm.prank(bnbWhale);
    SafeERC20.safeTransfer(moula, to, amount);

    vm.startPrank(to);
    //moula.mint(amount);
    //moula.approve(address(kairos), amount);
    SafeERC20.safeApprove(moula, address(kairos), amount);
    vm.stopPrank();
  }

  // @audit taken from IntegrationTest.testSimpleLoan (SimpleLoans.t.sol)
  // and adapted to demonstrate lost funds
  function testUnableToClaim() public {
    // signer is the supplier
    getFlooz(BORROWER, money, 10 ether);
    uint256 borrowerInitialBalance = money.balanceOf(BORROWER);

    uint256 amountBorrowed = 1 ether;
    getFlooz(signer, money, amountBorrowed);

    uint256 signerInitialBalance = money.balanceOf(signer);
    OfferArg[] memory offerArgs = getOfferArgs();
    uint256 tokenId = nft.mintOneTo(BORROWER);
    vm.prank(BORROWER);
    nft.safeTransferFrom(BORROWER, address(kairos), tokenId, abi.encode(offerArgs));
    assertEq(
        money.balanceOf(signer),
        signerInitialBalance - amountBorrowed,
        "lender balance incorrect after loan"
    );
    assertEq(money.balanceOf(BORROWER), borrowerInitialBalance + amountBorrowed);
    assertEq(nft.ownerOf(1), address(kairos));
    skip(1 weeks);
    uint256 toRepay = amountBorrowed.mul(getTranche(0).mul(1 weeks)) + amountBorrowed;
    vm.prank(BORROWER);
    money.approve(address(kairos), toRepay);

    vm.prank(BORROWER);
    kairos.repay(oneInArray);

    // unable to claim funds and call reverts
    vm.prank(signer);
    vm.expectRevert();
    kairos.claim(oneInArray);

    // borrower paid
    assertEq(money.balanceOf(BORROWER), borrowerInitialBalance + amountBorrowed - toRepay);
    // lender/signer missing the toRepay amount
    assertEq(money.balanceOf(signer), signerInitialBalance - amountBorrowed);
    // amount stuck in contract
    assertEq(money.balanceOf(address(kairos)), toRepay);
  }
}
```

## Tool used

Foundry

## Recommendation

Use a popular library like OZ's `SafeERC20` [3] to transfer tokens.

## References

[0] https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/utils/Erc20CheckedTransfer.sol#L8
[1] https://medium.com/coinmonks/missing-return-value-bug-at-least-130-tokens-affected-d67bf08521ca
[2] https://gist.github.com/alpeware/514a3178b9bc76d2a1567cbdf87488ae#file-lostfunds-t-sol
[3] https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/utils/SafeERC20.sol