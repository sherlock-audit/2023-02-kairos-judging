dacian

high

# Loss Of Funds when using Tax On Transfer ERC20 tokens

## Summary
Kairos explicitly aims to support any ERC20 token, however some ERC20 tokens charge a tax on every transfer, and other prominent tokens such as [USDT Tether](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code) don't currently charge a transfer tax but may in the future.

Kairos doesn't account for ERC20 transfer taxes in its internal accounting, resulting in a loss of funds where Kairos will allow a Borrower to Repay a loan but then the Lender will be unable to Claim as Claim() will revert due to "transfer amount exceeds balance", since Kairos will have insufficient balance due to the transfer tax which it didn't account for.

This issue should be considered a high vulnerability due to Kairos being automatically impacted when Kairos is used by any token which currently implements or may in the future (eg USDT) implement tax on transfer.

## Vulnerability Detail
Kairos doesn't account for ERC20 tokens implementing transfer taxes in any of its internal accounting, eg [RepayFacet.repay() L57](https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/RepayFacet.sol#L57))

## Impact
Loss of funds for lenders due to inconsistent internal state resulting from loans that were seemingly repaid but then aren't claimable as Kairos has insufficient funds so Claim() reverts.

## Code Snippet
First extend Money.sol to implement testing of tax on transfer tokens:

```solidity
contract Money is TestCurrency {
    /* solhint-disable-next-line no-empty-blocks */
    constructor() TestCurrency("Money", "MON") {
        // uncomment & run entire test suite to see many tests that fail
        // when used with a tax transfer token
        //transferTax = 1; 
    }

    // some ERC20 tokens have tax on transfer, so actual amount received
    // differs from given input. As Kairos accepts arbitrary ERC20s, it 
    // must account for TaxOnTransfer tokens. this extension to Money class 
    // will be used in the automated tests to verify this
    uint public accumulatedTransferFees;
    uint public transferTax; // 1 = 1%

    function setTransferTax(uint tax) public {
        transferTax = tax;
    }

    function getTaxOnAmount(uint amount) public view returns (uint) {
        return (amount * transferTax) / 100;
    }

    function _transfer(address from, address to, uint256 amount) internal virtual override {
        if(transferTax > 0) {
            uint256 taxAmount = getTaxOnAmount(amount);
            accumulatedTransferFees += taxAmount;
            
            super._transfer(from, address(this), taxAmount);
            super._transfer(from, to, amount - taxAmount);
        }
        else {
            super._transfer(from, to, amount);
        }
    }
}
```

Then add the following test to SimpleLoans.t.sol:

```solidity
function testSimpleLoanTransferTaxToken() public {
    money.setTransferTax(10); // 10% transfer tax
    uint BORROWED_AMOUNT       = 1 ether;

    // kairos starts with 0 tokens
    assertEq(money.balanceOf(address(kairos)), 0 ether);

    // borrower starts with 0 tokens
    assertEq(money.balanceOf(BORROWER), 0 ether);

    // mint BORROWED_AMOUNT to signer (lender)   
    getFlooz(signer, money, BORROWED_AMOUNT);
    assertEq(money.balanceOf(signer), BORROWED_AMOUNT);

    // create the lender's offer of loan for an NFT
    // loan amount set to 1 ether tokens
    OfferArg[] memory offerArgs = getOfferArgs();

    // mint the NFT to the borrower which will be used as collateral for the loan
    uint tokenId = nft.mintOneTo(BORROWER);

    // transfer the NFT from borrower to Kairos which will initiate the loan
    vm.prank(BORROWER);
    nft.safeTransferFrom(BORROWER, address(kairos), tokenId, abi.encode(offerArgs));

    // lender will have no balance as all was lent out
    assertEq(money.balanceOf(signer), 0, "lender balance incorrect after loan");

    // actual borrowed amount will be less than requested due to transfer tax
    uint ACTUAL_BORROWED_AMOUNT = BORROWED_AMOUNT - money.getTaxOnAmount(BORROWED_AMOUNT);

    assertEq(money.balanceOf(BORROWER), ACTUAL_BORROWED_AMOUNT);

    // kairos will hold the nft collateral                        
    assertEq(nft.ownerOf(1), address(kairos));

    // 1 day passes
    skip(1 days);

    // kairos calculates loan repayment based upon total loan amount, doesn't factor in
    // transfer tax, that actual amount received was less than requested amount
    uint REPAYMENT_AMOUNT = BORROWED_AMOUNT.mul(getTranche(0).mul(1 days)) + BORROWED_AMOUNT;

    // borrower went and did some trading, managed to accumulate the difference
    // between what they received in the loan, and the amount to be repaid
    getFlooz(BORROWER, money, REPAYMENT_AMOUNT-ACTUAL_BORROWED_AMOUNT);
    assertEq(money.balanceOf(BORROWER), REPAYMENT_AMOUNT);

    // approve Kairos to use borrower's tokens
    vm.prank(BORROWER);
    money.approve(address(kairos), REPAYMENT_AMOUNT);

    // kairos allows borrower to repay the loan, even though the actual amount received
    // will be less than the loan+interest required due to the transfer tax
    vm.prank(BORROWER);
    kairos.repay(oneInArray);

    // borrower has nothing left
    assertEq(money.balanceOf(BORROWER), 0 ether);

    // kairos has less than the repayment amount due to the transfer tax deduction that
    // kairos does not account for - this is an error
    assertEq(money.balanceOf(address(kairos)), REPAYMENT_AMOUNT - money.getTaxOnAmount(REPAYMENT_AMOUNT));

    // now signer will attempt to claim and this will revert as kairos has less tokens
    // than what it should due to not accounting for the transfer tax
    vm.prank(signer);
    kairos.claim(oneInArray); // reverts with "transfer amount exceeds balance"
}
```

Then execute the test:

```solidity
forge test --match "testSimpleLoanTransferTaxToken" -vvv
```

## Tool used

Manual Review

## Recommendation
In the updated Money.sol constructor, uncomment the transferTax = 1; & run all the unit tests, you will see many fail, this issue has widespread consequences.

Kairos in all of its internal accounting should take into account possible transfer taxes. For example [RepayFacet.repay() L57](https://github.com/sherlock-audit/2023-02-kairos/blob/main/kairos-contracts/src/RepayFacet.sol#L57) could be changed to check Kairos balance before & after the payment, to ensure that a sufficient amount was received after the transfer tax is deducted:

```solidity
// account for possible tax on transfer erc20 tokens
uint balancePre = loan.assetLent.balanceOf(address(this));
loan.assetLent.checkedTransferFrom(msg.sender, address(this), toRepay);
uint actualReceived = loan.assetLent.balanceOf(address(this)) - balancePre;
// actual amount received must be greater or equal to loan repayment
require(actualReceived >= toRepay, "Repayment insufficient due to transfer tax");
```

